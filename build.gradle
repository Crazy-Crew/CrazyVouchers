plugins {
    alias(libs.plugins.modrinth)

    id 'java-library'
}

assemble {
    subprojects.forEach { project -> dependsOn ":${project.name}:build" }

    doLast {
        File dir = new File(rootDir, "jars")

        if (dir.exists()) dir.delete()

        dir.mkdirs()

        copy {
            from(project("paper").layout.buildDirectory.file("libs/${rootProject.name}-${rootProject.version}.jar").get())
            into(dir)
        }
    }
}

subprojects {
    apply plugin: 'java-library'

    repositories {
        maven { url = 'https://repo.crazycrew.us/releases/' }

        maven { url = 'https://jitpack.io/' }

        mavenCentral()
    }

    if (name == "paper") {
        repositories {
            maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }

            maven { url = 'https://repo.codemc.io/repository/maven-public/' }

            maven { url = 'https://repo.triumphteam.dev/snapshots' }

            maven { url = 'https://repo.oraxen.com/releases/' }

            flatDir { dirs("libs") }
        }
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.release.set(17)
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    javadoc {
        options.encoding = 'UTF-8'
    }

    processResources {
        filteringCharset = 'UTF-8'
    }
}